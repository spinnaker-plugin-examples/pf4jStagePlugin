buildscript {
  ext.kotlin_version = '1.4.10'
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.yaml:snakeyaml:1.19"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

plugins {
  id("io.spinnaker.plugin.bundler").version("$spinnakerGradleVersion")
  id("com.palantir.git-version").version("0.12.2")
  id("com.diffplug.spotless").version("5.1.0")
}
apply plugin: 'kotlin'

repositories {
  mavenCentral()
}

spinnakerBundle {
  pluginId = "Armory.RandomWaitPlugin"
  description = "An example of a PF4J-based plugin that provides a custom pipeline stage."
  provider = "https://github.com/spinnaker-plugin-examples"
  version = rootProject.version
}

version = normalizedVersion()

subprojects {
  group = "io.armory.plugin.stage.wait.random"
  version = rootProject.version

  if (name != "random-wait-deck") {
    apply plugin: "com.diffplug.spotless"
    spotless {
      kotlin {
        ktlint().userData([
          disabled_rules          : "no-wildcard-imports",
          indent_size             : "2",
          continuation_indent_size: "2",
        ])
      }
    }
  }
}
dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}
compileKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}
compileTestKotlin {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}

String normalizedVersion() {
  String fullVersion = gitVersion()
  String normalized = fullVersion.split("-").first()
  if (fullVersion.contains("dirty")) {
    return "$normalized-SNAPSHOT"
  } else {
    return normalized
  }
}

