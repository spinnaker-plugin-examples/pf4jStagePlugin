buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    classpath "org.yaml:snakeyaml:1.19"
  }
}

plugins {
  id("io.spinnaker.plugin.bundler").version("$spinnakerGradleVersion")
  id("com.palantir.git-version").version("0.12.2")
  id("org.jetbrains.kotlin.jvm").version("$kotlinVersion")
  id("org.jetbrains.kotlin.kapt").version("$kotlinVersion")
  id("com.diffplug.spotless").version("5.1.0")
}

repositories {
  mavenCentral()
}

spinnakerBundle {
  pluginId = "Armory.RandomWaitPlugin"
  description = "An example of a PF4J-based plugin that provides a custom pipeline stage."
  provider = "https://github.com/spinnaker-plugin-examples"
  version = rootProject.version
}

version = normalizedVersion()

subprojects {
  group = "io.armory.plugin.stage.wait.random"
  version = rootProject.version

  if (name != "random-wait-deck") {
    apply plugin: "com.diffplug.spotless"
    spotless {
      kotlin {
        ktlint().userData([
          disabled_rules          : "no-wildcard-imports",
          indent_size             : "2",
          continuation_indent_size: "2",
        ])
      }
    }
  }
}

String normalizedVersion() {
  String fullVersion = gitVersion()
  String normalized = fullVersion.split("-").first()
  if (fullVersion.contains("dirty")) {
    return "$normalized-SNAPSHOT"
  } else {
    return normalized
  }
}

